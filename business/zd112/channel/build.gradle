if(IS_CHANNEL_APP.toBoolean()){
  apply plugin: 'com.android.application'
}else{
  apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
android {

  signingConfigs {
    sign {
      storeFile file("$rootDir/$STORE_FILE")
      storePassword STORE_PSW
      keyAlias = KEY_ALIAS
      keyPassword KEY_PSW

      v1SigningEnabled = true
      v2SigningEnabled = true
    }
  }

  compileSdkVersion COMPILE_SDK_VERSION.toInteger()
  buildToolsVersion SUPPORT_SDK_VERSION

  defaultConfig {

    if(IS_CHANNEL_APP.toBoolean()){
      applicationId "com.android.channel"
    }

    minSdkVersion MIN_SDK_VERSION.toInteger()
    targetSdkVersion TARGET_SDK_VERSION.toInteger()
    versionCode 1
    versionName "1.0"

    multiDexEnabled true

  }

  sourceSets.main{
    if(IS_CHANNEL_APP.toBoolean()){
      manifest.srcFile 'src/main/app/AndroidManifest.xml'
    }else{
      manifest.srcFile 'src/main/AndroidManifest.xml'
    }
  }

  buildTypes {
    debug {
      minifyEnabled false
      signingConfig signingConfigs.sign
    }

    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.sign
    }
  }

  compileOptions {
    sourceCompatibility = JAVA_VERSION
    targetCompatibility = JAVA_VERSION
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation project(':resource')
  implementation project(':base')
  implementation project(':blog')
  implementation project(':entity')
}
