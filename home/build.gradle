if(IS_HOME_APP.toBoolean()){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}

android {

    signingConfigs {
        sign {
            storeFile file("$rootDir/$STORE_FILE")
            storePassword STORE_PSW
            keyAlias = KEY_ALIAS
            keyPassword KEY_PSW

            v1SigningEnabled = true
            v2SigningEnabled = true
        }
    }

    compileSdkVersion COMPILE_SDK_VERSION.toInteger()
    if(IS_PREFIX.toBoolean()){
        resourcesPrefix "home"
    }

    defaultConfig {
        if(IS_HOME_APP.toBoolean()){
            applicationId "com.android.home"
        }
        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TARGET_SDK_VERSION.toInteger()
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    sourceSets.main{
        if(IS_HOME_APP.toBoolean()){
            manifest.srcFile 'src/main/app/AndroidManifest.xml'
        }else{
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.sign
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
    }

    if(IS_HOME_APP.toBoolean()){
        assemble.doLast {
            android.applicationVariants.all { variant ->
                if (variant.name == "release") {
                    variant.outputs.each { output ->
                        File originFile = output.outputFile
                        println originFile.absolutePath
                        copy {
                            from originFile
                            into "$rootDir/host/src/main/assets"
                        }
                    }
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    annotationProcessor project(path: ':ZdAnnotations_Compile')
    implementation project(path: ':widget')
    implementation project(path: ':ZdAnnotations')
    implementation project(path: ':ZdRouter')
    implementation project(path: ':base')
    implementation project(path: ':utils')
    implementation project(path: ':event')
    implementation project(path: ':ZdPlugin')
    if(!IS_SKIN_APP.toBoolean()){
        implementation project(path: ':skin')
    }
}
